stimulus_display d1 (
    display_info = {
        'display_to_use': 0,
        'width': 400,
        'height': 300,
        'distance': 100
        }
    background_color = 1,0,0
    )


var elapsed_time = 0
var frame_count = 0
var done = false

var expected_refresh_rate = 0.0
var measured_refresh_rate = 0.0


render_actions frame_counter (
    elapsed_time = elapsed_time
    display = d1
    ) {
    if (frame_count == 500) {
        measured_refresh_rate = 1e6 * frame_count / elapsed_time
        done = true
    }
    frame_count += 1
}


protocol {
    task {
        state Begin {
            expected_refresh_rate = refresh_rate('d1')
            assert (expected_refresh_rate > 0.0)

            frame_count = 0
            done = false

            queue_stimulus (frame_counter)
            update_display (display = d1)

            goto (
                target = End
                when = done
                )
        }

        state End {
            clear_display (display = d1)

            report ('Expected $expected_refresh_rate Hz, measured $measured_refresh_rate Hz')

            // Accept no more than 0.1% difference between expected and measured
            assert (abs(measured_refresh_rate - expected_refresh_rate) / expected_refresh_rate <= 0.001)

            yield ()
        }
    }
}
